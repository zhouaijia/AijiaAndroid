plugins {
    //id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
}

if (rootProject.ext.isModule) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    namespace 'com.aijia.main'
    compileSdk rootProject.android.compileSdk

    defaultConfig {
        if (rootProject.ext.isModule) {
            applicationId "com.aijia.main" //这里不指定applicationId也不会报错
            targetSdk rootProject.android.targetSdk
            versionCode rootProject.android.versionCode
            versionName rootProject.android.versionName
        } else {
            consumerProguardFiles "consumer-rules.pro"
        }
        minSdk rootProject.android.minSdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        kapt {
            arguments {
                arg("AROUTER_MODULE_NAME", project.getName())
            }
        }

    }

    //模式切换时资源也同步切换
    sourceSets {
        main {
            if (rootProject.ext.isModule) {
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'

                //library模式下排除debug文件夹下所有的java文件
                java {
                    exclude 'debug/**'
                }
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        dataBinding = true
        viewBinding = true
    }
}

dependencies {

    implementation rootProject.depsLibs.coreKtx
    implementation rootProject.depsLibs.appcompat
    implementation rootProject.depsLibs.material
    implementation rootProject.depsLibs.constraintlayout
    implementation rootProject.depsLibs.navigationFragment
    implementation rootProject.depsLibs.navigationUI
    implementation rootProject.depsLibs.legacy
    implementation rootProject.depsLibs.liveDataKtx
    implementation rootProject.depsLibs.viewModelKtx
    testImplementation rootProject.depsLibs.junit
    androidTestImplementation rootProject.depsLibs.extJunit
    androidTestImplementation rootProject.depsLibs.espressoCore

    compileOnly rootProject.depsLibs.arouterApi
    kapt rootProject.depsLibs.arouterCompiler

    implementation project(':lib_framework')
    compileOnly project(':lib_common')
    implementation project(':lib_log')
}